import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Ros2
import RQml.Elements
import "elements"
import "interfaces"

Rectangle {
    id: root
    enum State {
        Unknown,
        Unconfigured,
        Inactive,
        Active,
        Finalized
    }

    // Test hook: exposes the ControllerManagerInterface so tests can drive
    // transitions without having to synthesize right-clicks on delegates.
    property alias controllerManagerInterface: d.controllerManager
    property var kddockwidgets_min_size: Qt.size(350, 500)

    anchors.fill: parent
    color: palette.base

    Component.onCompleted: {
        if (context.enabled === undefined)
            context.enabled = true;
        d.refresh();
    }

    GridLayout {
        anchors.fill: parent
        anchors.margins: 4
        columns: 3

        Label {
            text: "Controller Manager"
        }
        ComboBox {
            id: controllerManagerComboBox
            Layout.fillWidth: true
            model: d.controllerManagers
            objectName: "cmComboBox"

            onCurrentValueChanged: {
                if (!currentValue || currentValue === context.controller_manager_namespace)
                    return;
                context.controller_manager_namespace = currentValue;
            }
        }
        RefreshButton {
            objectName: "cmRefreshButton"

            onClicked: {
                animate = true;
                d.refresh();
                animate = false;
            }
        }
        LoadingListView {
            id: controllerListView
            Layout.columnSpan: 3
            Layout.fillHeight: true
            Layout.fillWidth: true
            Layout.preferredHeight: 240
            headerPositioning: ListView.OverlayHeader
            isLoading: d.controllerManager.loading
            model: d.controllerManager.controllers
            objectName: "cmControllerList"
            spacing: 8

            delegate: MouseArea {
                property var controller: model

                acceptedButtons: Qt.LeftButton | Qt.RightButton
                height: 48 // With Qt 6.9 ContextMenu could be used, but not available before
                width: controllerListView.width

                onClicked: mouse => {
                    if (mouse.button === Qt.RightButton) {
                        contextMenu.popup();
                    }
                }
                onPressAndHold: mouse => {
                    if (mouse.source === Qt.MouseEventNotSynthesized) {
                        contextMenu.popup();
                    }
                }

                Menu {
                    id: contextMenu
                    width: {
                        let result = 0;
                        let padding = 0;
                        for (let i = 0; i < count; ++i) {
                            let item = itemAt(i);
                            result = Math.max(item.contentItem.implicitWidth, result);
                            padding = Math.max(item.padding, padding);
                        }
                        return result + padding * 2;
                    }

                    Instantiator {
                        model: d.getTransitionsForControllerState(controller.state)

                        delegate: MenuItem {
                            text: modelData.name

                            onTriggered: {
                                d.controllerManager.transitionController(controller.name, modelData.actions);
                            }
                        }

                        onObjectAdded: (index, object) => contextMenu.insertItem(index, object)
                        onObjectRemoved: (index, object) => contextMenu.removeItem(object)
                    }
                    MenuSeparator {
                    }
                    Action {
                        text: qsTr("Show Info")

                        onTriggered: {
                            controllerInfoDialog.openControllerInfo(controller);
                        }
                    }
                }
                RowLayout {
                    anchors.fill: parent
                    anchors.rightMargin: controllerListView.ScrollBar.vertical.visible ? controllerListView.ScrollBar.vertical.width : 0

                    StateIndicator {
                        Layout.alignment: Qt.AlignVCenter
                        Layout.margins: 4
                        state: model.state
                    }
                    Label {
                        Layout.fillWidth: true
                        text: model.name
                    }
                    Label {
                        Layout.margins: 8
                        text: model.state
                    }
                    Button {
                        Layout.margins: 4
                        implicitHeight: parent.height - 8
                        implicitWidth: 40
                        text: "..."

                        onClicked: contextMenu.popup()
                    }
                }
            }
            header: ListHeader {
                text: "Controllers"
            }
        }
        LoadingListView {
            id: hardwareComponentsListView
            Layout.columnSpan: 3
            Layout.fillHeight: true
            Layout.fillWidth: true
            Layout.preferredHeight: 120
            Layout.topMargin: 8
            headerPositioning: ListView.OverlayHeader
            isLoading: d.controllerManager.loading
            model: d.controllerManager.hardwareComponents
            objectName: "cmHardwareList"
            spacing: 8

            delegate: MouseArea {
                property var hardwareComponent: model

                acceptedButtons: Qt.LeftButton | Qt.RightButton
                height: 48 // With Qt 6.9 ContextMenu could be used, but not available before
                width: hardwareComponentsListView.width

                onClicked: mouse => {
                    if (mouse.button === Qt.RightButton) {
                        contextMenu.popup();
                    }
                }
                onPressAndHold: mouse => {
                    if (mouse.source === Qt.MouseEventNotSynthesized) {
                        contextMenu.popup();
                    }
                }

                Menu {
                    id: contextMenu
                    width: {
                        let result = 0;
                        let padding = 0;
                        for (let i = 0; i < count; ++i) {
                            let item = itemAt(i);
                            result = Math.max(item.contentItem.implicitWidth, result);
                            padding = Math.max(item.padding, padding);
                        }
                        return result + padding * 2;
                    }

                    Instantiator {
                        model: d.getTransitionsForHardwareComponentState(hardwareComponent.state.label)

                        delegate: MenuItem {
                            text: modelData.name

                            onTriggered: {
                                d.controllerManager.transitionHardwareComponent(hardwareComponent.name, modelData.target_state);
                            }
                        }

                        onObjectAdded: (index, object) => contextMenu.insertItem(index, object)
                        onObjectRemoved: (index, object) => contextMenu.removeItem(object)
                    }
                    MenuSeparator {
                    }
                    Action {
                        text: qsTr("Show Info")

                        onTriggered: {
                            hardwareComponentInfoDialog.openHardwareComponentInfo(hardwareComponent);
                        }
                    }
                }
                RowLayout {
                    anchors.fill: parent
                    anchors.rightMargin: hardwareComponentsListView.ScrollBar.vertical.visible ? hardwareComponentsListView.ScrollBar.vertical.width : 0

                    StateIndicator {
                        Layout.alignment: Qt.AlignVCenter
                        Layout.margins: 4
                        state: model.state.label
                    }
                    Label {
                        Layout.fillWidth: true
                        text: model.name
                    }
                    Label {
                        Layout.margins: 8
                        text: model.state.label
                    }
                    Button {
                        Layout.margins: 4
                        implicitHeight: parent.height - 8
                        implicitWidth: 40
                        text: "..."

                        onClicked: contextMenu.popup()
                    }
                }
            }
            header: ListHeader {
                text: "Hardware Components"
                z: 2
            }
        }
    }
    ControllerInfoDialog {
        id: controllerInfoDialog
        objectName: "cmControllerInfoDialog"
    }
    HardwareComponentInfoDialog {
        id: hardwareComponentInfoDialog
        objectName: "cmHardwareComponentInfoDialog"
    }
    QtObject {
        id: d

        property var controllerManager: ControllerManagerInterface {
            controllerManager: context.controller_manager_namespace || ""
        }
        property var controllerManagers: []
        property var trajectoryClient: null

        function getTransitionsForControllerState(state) {
            const transitions = {
                "active": [{
                        "name": "Deactivate (inactive)",
                        "actions": ["deactivate"]
                    }, {
                        "name": "Deactivate and Unload (unloaded)",
                        "actions": ["deactivate", "unload"]
                    }],
                "inactive": [{
                        "name": "Activate (active)",
                        "actions": ["activate"]
                    }, {
                        "name": "Unload and Load (unconfigured)",
                        "actions": ["unload", "load"]
                    }, {
                        "name": "Unload (unloaded)",
                        "actions": ["unload"]
                    }],
                "unconfigured": [{
                        "name": "Configure and Activate (active)",
                        "actions": ["configure", "activate"]
                    }, {
                        "name": "Configure (inactive)",
                        "actions": ["configure"]
                    }, {
                        "name": "Unload (unloaded)",
                        "actions": ["unload"]
                    }],
                "unloaded": [{
                        "name": "Load (unconfigured)",
                        "actions": ["load"]
                    }]
            };
            return transitions[state] || [];
        }
        function getTransitionsForHardwareComponentState(state) {
            const transitions = {
                "active": [{
                        "name": "Deactivate (inactive)",
                        "target_state": {
                            "id": State.Inactive,
                            "label": "inactive"
                        }
                    }, {
                        "name": "Deactivate and Cleanup (unconfigured)",
                        "target_state": {
                            "id": State.Unconfigured,
                            "label": "unconfigured"
                        }
                    },],
                "inactive": [{
                        "name": "Activate (active)",
                        "target_state": {
                            "id": State.Active,
                            "label": "active"
                        }
                    }, {
                        "name": "Cleanup (unconfigured)",
                        "target_state": {
                            "id": State.Unconfigured,
                            "label": "unconfigured"
                        }
                    },],
                "unconfigured": [{
                        "name": "Configure and Activate (active)",
                        "target_state": {
                            "id": State.Active,
                            "label": "active"
                        }
                    }, {
                        "name": "Configure (inactive)",
                        "target_state": {
                            "id": State.Inactive,
                            "label": "inactive"
                        }
                    },]
            };
            return transitions[state] || [];
        }
        function refresh() {
            const prevControllerManager = context.controller_manager_namespace;
            const services = Ros2.queryServices("controller_manager_msgs/srv/ListControllers");
            let controllerManagers = prevControllerManager ? [prevControllerManager] : [];
            for (let i = 0; i < services.length; i++) {
                const parts = services[i].split("/");
                parts.pop(); // remove service name
                const ns = parts.join("/");
                if (controllerManagers.indexOf(ns) === -1) {
                    controllerManagers.push(ns);
                }
            }
            // Remove empty entries
            controllerManagers = controllerManagers.filter(function (e) {
                    return e;
                });
            controllerManagers.sort();
            d.controllerManagers = [];
            d.controllerManagers = controllerManagers;
            if (prevControllerManager) {
                const index = d.controllerManagers.indexOf(prevControllerManager);
                controllerManagerComboBox.currentIndex = Math.max(0, index);
                d.controllerManager.refresh();
            }
        }
    }
}
