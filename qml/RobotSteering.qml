import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Ros2
import RQml.Elements
import RQml.Fonts

Rectangle {
    id: root

    property var kddockwidgets_min_size: Qt.size(350, 500)

    anchors.fill: parent
    color: palette.base

    Component.onCompleted: {
        if (!context.linear)
            context.linear = {};
        if (!context.angular)
            context.angular = {};
    }

    QtObject {
        id: d

        property var publisher: {
            if (!context.topic || !Ros2.isValidTopic(context.topic))
                return null;
            return Ros2.createPublisher(context.topic, context.stamped ? "geometry_msgs/msg/TwistStamped" : "geometry_msgs/msg/Twist", 1);
        }

        function publish() {
            if (!context.enabled || !publisher)
                return;
            if (stampedCheckBox.checked) {
                publisher.publish({
                        "header": {
                            "stamp": Ros2.now()
                        },
                        "twist": {
                            "linear": {
                                "x": linearSlider.value,
                                "y": 0,
                                "z": 0
                            },
                            "angular": {
                                "x": 0,
                                "y": 0,
                                "z": angularSlider.value
                            }
                        }
                    });
            } else {
                publisher.publish({
                        "linear": {
                            "x": linearSlider.value,
                            "y": 0,
                            "z": 0
                        },
                        "angular": {
                            "x": 0,
                            "y": 0,
                            "z": angularSlider.value
                        }
                    });
            }
        }
    }
    Timer {
        interval: rate.value == 0 ? 0 : 1000 / rate.value
        repeat: true
        running: rate.value > 0

        onTriggered: d.publish()
    }
    ColumnLayout {
        anchors.fill: parent
        anchors.margins: 8

        RowLayout {
            FuzzySelector {
                id: topicSelect
                function refresh() {
                    let topics = Ros2.queryTopics("geometry_msgs/msg/Twist");
                    let stampedTopics = Ros2.queryTopics("geometry_msgs/msg/TwistStamped");
                    topics = topics.concat(stampedTopics);
                    // Deduplicate (a topic could appear in both)
                    topics = [...new Set(topics)];
                    topics.sort();
                    if (!!context.topic) {
                        const index = topics.indexOf(context.topic);
                        if (index !== -1)
                            topics.splice(index, 1);
                        topics.unshift(context.topic);
                    }
                    model = topics;
                }

                Layout.fillWidth: true
                objectName: "steeringTopicSelector"
                placeholderText: qsTr("Velocity Topic")

                Component.onCompleted: {
                    text = context.topic ?? "/cmd_vel";
                    refresh();
                }
                onTextChanged: {
                    if (text === context.topic)
                        return;
                    context.enabled = false;
                    context.topic = text;
                    if (!Ros2.isValidTopic(text))
                        return;
                    const types = Ros2.queryTopicTypes(text);
                    const hasTwist = types.includes("geometry_msgs/msg/Twist");
                    const hasStamped = types.includes("geometry_msgs/msg/TwistStamped");
                    if (hasTwist && !hasStamped)
                        stampedCheckBox.checked = false;
                    else if (hasStamped && !hasTwist)
                        stampedCheckBox.checked = true;
                }
            }
            RefreshButton {
                onClicked: {
                    animate = true;
                    topicSelect.refresh();
                    animate = false;
                }
            }
        }
        RowLayout {
            spacing: 10

            CheckBox {
                id: stampedCheckBox
                checked: !!context.stamped
                display: AbstractButton.TextUnderIcon
                objectName: "steeringStampedCheckbox"
                text: "Stamped"

                onCheckedChanged: context.stamped = checked
            }
            Label {
                text: "Rate (Hz):"
            }
            SpinBox {
                id: rate
                editable: true
                from: 0
                objectName: "steeringRateSpinBox"
                stepSize: 1
                to: 100

                Component.onCompleted: value = context.rate ?? 10
                onValueChanged: context.rate = value
            }
            // Spacer
            Item {
                Layout.fillWidth: true
            }
            Button {
                id: playButton
                ToolTip.delay: 500
                ToolTip.text: checked ? "Click to pause" : "Click to start"
                ToolTip.visible: hovered
                checkable: true
                checked: !!context.enabled
                font.family: IconFont.name
                font.pixelSize: 20
                implicitHeight: 48
                implicitWidth: 48
                objectName: "steeringPlayButton"
                text: checked ? IconFont.iconPause : IconFont.iconPlay

                onCheckedChanged: context.enabled = checked
            }
        }
        SpeedSlider {
            id: linearSlider
            Layout.alignment: Qt.AlignHCenter
            Layout.fillHeight: true
            enabled: !!context.enabled
            from: context.linear.min ?? -1.0
            objectName: "steeringLinearSlider"
            to: context.linear.max ?? 1.0

            onFromChanged: context.linear.min = from
            onToChanged: context.linear.max = to
            onValueChanged: {
                if (rate.value == 0)
                    publish();
            }
        }
        SpeedSlider {
            id: angularSlider
            Layout.fillWidth: true
            direction: Qt.Horizontal
            enabled: !!context.enabled
            from: context.angular.min ?? -1.0
            objectName: "steeringAngularSlider"
            to: context.angular.max ?? 1.0

            onFromChanged: context.angular.min = from
            onToChanged: context.angular.max = to
            onValueChanged: {
                if (rate.value == 0)
                    publish();
            }
        }
        Button {
            Layout.fillWidth: true
            objectName: "steeringStopButton"
            text: "Stop"

            onClicked: {
                linearSlider.value = 0;
                angularSlider.value = 0;
            }
        }
    }
}
